//created on: May 16, 2021
package rules

//list any import classes here.
import com.sbnz.helpers.Utility;
import com.sbnz.model.RegisteredUser;
import com.sbnz.model.Destination;
import com.sbnz.model.Category;
import com.sbnz.model.Hotel;
import com.sbnz.dto.SearchDTO;
import com.sbnz.model.LocalFood;
import com.sbnz.model.Transportation;


rule "Add points to destinations which are user interests"
	agenda-group "default"
	no-loop true
	lock-on-active true
    when
        $user: RegisteredUser($interests: interests)
        $destination: Destination($categories: categories)
        accumulate( Category( $c: this , this memberOf $categories) from $interests,
        			 $cats: collectList( $c ))
    then
		modify($destination) {
			setScore($destination.getScore() + 5*$cats.size());	
			
        }		
        System.out.println(""+5*$cats.size()+" points goes to "+$destination.getName()+" for having user interests");
end

rule "Add points to destinations for younger population"
    agenda-group "default"
    no-loop
	lock-on-active true
    when
        $user: RegisteredUser(Utility.getYears(dateOfBirth) < 30)
        $destination: Destination($categories: categories)
        Number($value: intValue >= 1) from accumulate(
        	Category($n: name,  ["shopping", "night life", "city", "adventure", "concerts"] contains $n ) from $categories,
        	count($n)
        )
    then
    	modify($destination) {
			setScore($destination.getScore() + 5 * $value);		
		}
		System.out.println(""+5 * $value+" points goes to "+$destination.getName()+" for younger population");
end

rule "Add points to destinations for older population"
    agenda-group "default"
    no-loop
	lock-on-active true
    when
        $user: RegisteredUser(Utility.getYears(dateOfBirth) > 50)
        $destination: Destination($categories: categories)
        Number($value: intValue >= 1) from accumulate(
        	Category($n: name, ["museums", "history", "city", "mountains", "spa"] contains $n) from $categories,
        	count($n)
        )
    then
    	modify($destination) {
			setScore($destination.getScore() + 5 * $value);	
		}
		System.out.println(""+5 * $value+" points goes to "+$destination.getName()+" for older population");
end


