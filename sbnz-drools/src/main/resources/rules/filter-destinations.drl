//created on: May 16, 2021
package rules

//list any import classes here.
import com.sbnz.helpers.Utility;
import com.sbnz.model.RegisteredUser;
import com.sbnz.model.Destination;
import com.sbnz.model.Category;
import com.sbnz.model.Hotel;
import com.sbnz.dto.SearchDTO;
import com.sbnz.model.LocalFood;
import com.sbnz.model.Transportation;

// TODO pravilo za max distance
rule "Add points to destinations with distance smaller than user's max distance"
    agenda-group "distance"
    no-loop true
	lock-on-active true
    when
        $search: SearchDTO($maxDistance: maxDistance)  
        $destination: Destination($lat1: locationLat, $lon1: locationLon)
        $user: RegisteredUser($userLat: locationLat, $userLon: locationLon)
    
    	$distance: Double() from Utility.distance($lat1, $lon1, $userLat, $userLon)
    	eval($distance <= $maxDistance)
    then
		modify($destination) {
			setScore($destination.getScore() + 5);	
		}
	    System.out.println("10 points goes to "+$destination.getName()+" for fitting into user's maximum distance: "+$maxDistance);	
       
end

rule "Predict user's budget based on profession (student)"
    agenda-group "budget"
    no-loop true
	lock-on-active true
    when
        $user: RegisteredUser(profession == "student")
    	$search: SearchDTO($budget: budget)  
    	eval($budget == null)
    then
		modify($search) {
			setBudget("medium");	
	    }
	    System.out.println("Predicted budget is medium");	
end

rule "Predict user's budget based on profession (employed)"
    agenda-group "budget"
    no-loop true
	lock-on-active true
    when
        $user: RegisteredUser(profession=="employed")
    	$search: SearchDTO($budget: budget)  
    	eval($budget == null)
    then
		modify($search) {
			setBudget("high");	
	    }
	    System.out.println("Predicted budget is high");	
end

rule "Predict user's budget based on profession (unemployed)"
    agenda-group "budget"
    no-loop true
	lock-on-active true
    when
        $user: RegisteredUser(profession=="unemployed")
    	$search: SearchDTO($budget: budget)
    	eval($budget == null)
    then
		modify($search) {
			setBudget("low");	
	    }	
	    System.out.println("Predicted budget is low");
end

rule "Add points to destinations with high budget"
    agenda-group "budget"
    no-loop true
    when
        $search: SearchDTO($transport: transportation, $numberOfPeople: numberOfPeople, budget == "high")  
        $destination: Destination($lat1: locationLat, $lon1: locationLon)
        $user: RegisteredUser($userLat: locationLat, $userLon: locationLon)
    
    	$distance: Double() from Utility.distance($lat1, $lon1, $userLat, $userLon)
    	$estimatedBudget: Double() from Utility.multiply($distance, $transport, $numberOfPeople)
    	eval($estimatedBudget > 2500)
    then
		modify($destination) {
			setScore($destination.getScore() + 10);	
	    }
	    System.out.println("10 points goes to "+$destination.getName()+" for fitting into user's budget - high, estimated budget: "+$estimatedBudget +"e");	
       
end

rule "Add points to destinations with medium budget"
    agenda-group "budget"
    no-loop true
    when
        $search: SearchDTO($transport: transportation, $numberOfPeople: numberOfPeople, budget == "medium")  
        $destination: Destination($lat1: locationLat, $lon1: locationLat)
        $user: RegisteredUser($userLat: locationLat, $userLon: locationLon)
    
    	$distance: Double() from Utility.distance($lat1, $lon1, $userLat, $userLon)
    	$estimatedBudget: Double() from Utility.multiply($distance, $transport, $numberOfPeople)
        eval($estimatedBudget < 2500 && $estimatedBudget > 1000)
	
    then
		modify($destination) {
			setScore($destination.getScore() + 10);	
	    }
	    System.out.println("10 points goes to "+$destination.getName()+" for fitting into user's budget - medium, estimated budget: "+$estimatedBudget +"e");	
end


rule "Add points to destinations with low budget"
    agenda-group "budget"
    no-loop true
    when
        $search: SearchDTO($transport: transportation, $numberOfPeople: numberOfPeople, budget == "low")  
        $destination: Destination($lat1: locationLat, $lon1: locationLon)
        $user: RegisteredUser($userLat: locationLat, $userLon: locationLon)
    
    	$distance: Double() from Utility.distance($lat1, $lon1, $userLat, $userLon)
    	$estimatedBudget: Double() from Utility.multiply($distance, $transport, $numberOfPeople)
    	eval($estimatedBudget < 2500)
    then
		modify($destination) {
			setScore($destination.getScore() + 10);	
		}
	    System.out.println("10 points goes to "+$destination.getName()+" for fitting into user's budget - low, estimated budget: "+$estimatedBudget +"e");	
       
end

rule "Add points to destinations with wanted accommodation level"
    agenda-group "accommodation"
    no-loop true
	lock-on-active true
    when
        $search: SearchDTO($accommodation: accommodation)
        $destination: Destination($hotels : hotels)
        accumulate( Hotel( $h: this , stars == $accommodation) from $hotels,
        			 $hotelsWithAccommodation: collectList( $h ))
    then	
		modify($destination) {
			setScore($destination.getScore() + 5*$hotelsWithAccommodation.size());	
        }
       System.out.println(""+5*$hotelsWithAccommodation.size()+" points goes to "+$destination.getName()+" for having hotels with "+$accommodation+" stars");	
end

rule "Add points to destinations with hotels which have children discount if user has children"
    agenda-group "children"
    no-loop true
	lock-on-active true
    when
        $search: SearchDTO($children: children)
        $destination: Destination($hotels : hotels)
        accumulate( Hotel( $h: this , childrenDiscount == $children) from $hotels,
        			 $hotelsWithDiscount: collectList( $h ))
        eval ($children == true)
    then  	
		modify($destination) {
			setScore($destination.getScore() + 5*$hotelsWithDiscount.size());	
	    }
		System.out.println(""+5*$hotelsWithDiscount.size()+" points goes to "+$destination.getName()+" for having hotels with children discount");
        		
end

rule "Add points to destinations with selected transportation"
    agenda-group "transportation"
    no-loop true
	lock-on-active true
    when
        $search: SearchDTO($transport: transportation)
        $destination: Destination(transportation contains $transport)
    then
		modify($destination) {
			setScore($destination.getScore() + 5);
        }		
        System.out.println("5 points goes to "+$destination.getName()+" for having transportation type: "+$transport);	
        
end

rule "Add points to destinations which are user interests"
	agenda-group "default"
	no-loop true
	lock-on-active true
    when
        $user: RegisteredUser($interests: interests)
        $destination: Destination($categories: categories)
        accumulate( Category( $c: this , this memberOf $categories) from $interests,
        			 $cats: collectList( $c ))
    then
		modify($destination) {
			setScore($destination.getScore() + 5*$cats.size());	
			
        }		
        System.out.println(""+5*$cats.size()+" points goes to "+$destination.getName()+" for having user interests");
end

rule "Add points to destinations with selected local food"
	agenda-group "food"
	no-loop true
	lock-on-active true
    when
        $search: SearchDTO($food: localFood)
        $destination: Destination($destinationFood: localFood)
        accumulate( LocalFood( $f: this , this memberOf $destinationFood) from $food,
        			 $foodList: collectList( $f ))
    then
		modify($destination) {
			setScore($destination.getScore() + 5*$foodList.size());	
			
        }		
        System.out.println(""+5*$foodList.size()+" points goes to "+$destination.getName()+" for having user's food options");
end

rule "Add points to destinations for younger population"
    agenda-group "default"
    no-loop
	lock-on-active true
    when
        $user: RegisteredUser(Utility.getYears(dateOfBirth) < 30)
        $destination: Destination($categories: categories)
        Number($value: intValue >= 1) from accumulate(
        	Category($n: name, $n == "shopping" || $n == "night life" || $n == "city"  || $n == "adventure" || $n == "concerts" ) from $categories,
        	count($n)
        )
    then
    	modify($destination) {
			setScore($destination.getScore() + 5 * $value);		
		}
		System.out.println(""+5 * $value+" points goes to "+$destination.getName()+" for younger population");
end

rule "Add points to destinations for older population"
    agenda-group "default"
    no-loop
	lock-on-active true
    when
        $user: RegisteredUser(Utility.getYears(dateOfBirth) > 50)
        $destination: Destination($categories: categories)
        Number($value: intValue >= 1) from accumulate(
        	Category($n: name, $n == "museums" || $n == "history" || $n == "city"  || $n == "mountains" || $n == "spa" ) from $categories,
        	count($n)
        )
    then
    	modify($destination) {
			setScore($destination.getScore() + 5 * $value);	
		}
		System.out.println(""+5 * $value+" points goes to "+$destination.getName()+" for older population");
end


