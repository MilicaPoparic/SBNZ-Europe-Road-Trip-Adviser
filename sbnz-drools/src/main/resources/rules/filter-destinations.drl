//created on: May 16, 2021
package rules

//list any import classes here.
import com.sbnz.helpers.Utility;
import com.sbnz.model.RegisteredUser;
import com.sbnz.model.Destination;
import com.sbnz.model.Category;
import com.sbnz.model.Hotel;
import com.sbnz.dto.SearchDTO;
import com.sbnz.model.LocalFood;
import com.sbnz.model.Transportation;

//declare any global variables here

rule "Add points to destinations with selected trasnportation"
    no-loop true
	lock-on-active true
    when
        $search: SearchDTO($transport: transportation)
        $destination: Destination(transportation contains $transport)
    then
		modify($destination) {
			setScore($destination.getScore() + 5);	
        }		
end

rule "Add points to destinations which are user interests"
	no-loop true
	lock-on-active true
    when
        $user: RegisteredUser($interests: interests)
        $destination: Destination($categories: categories)
        accumulate( Category( $c: this , this memberOf $categories) from $interests,
        			 $cats: collectList( $c ))
    then
		modify($destination) {
			setScore($destination.getScore() + 5*$cats.size());	
        }		
end

rule "Add points to destinations with selected local food"
	no-loop true
	lock-on-active true
    when
        $search: SearchDTO($food: localFood)
        $destination: Destination($destinationFood: localFood)
        accumulate( LocalFood( $f: this , this memberOf $destinationFood) from $food,
        			 $foodList: collectList( $f ))
    then
		modify($destination) {
			setScore($destination.getScore() + 5*$foodList.size());	
        }		
end

rule "Add points to destinations for younger population"
    no-loop
	lock-on-active true
    when
        $user: RegisteredUser(Utility.getYears(dateOfBirth) < 30)
        $destination: Destination($categories: categories)
        Number($value: intValue >= 1) from accumulate(
        	Category($n: name, $n == "shopping" || $n == "night life" || $n == "city"  || $n == "adventure" || $n == "concerts" ) from $categories,
        	count($n)
        )
    then
    	modify($destination) {
			setScore($destination.getScore() + 5 * $value);
			}
end

rule "Add points to destinations for older population"
    no-loop
	lock-on-active true
    when
        $user: RegisteredUser(Utility.getYears(dateOfBirth) > 50)
        $destination: Destination($categories: categories)
        Number($value: intValue >= 1) from accumulate(
        	Category($n: name, $n == "museums" || $n == "history" || $n == "city"  || $n == "mountains" || $n == "spa" ) from $categories,
        	count($n)
        )
    then
    	modify($destination) {
			setScore($destination.getScore() + 5 * $value);
			}
end


